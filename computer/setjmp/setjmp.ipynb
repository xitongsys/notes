{
 "cells": [
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# setjmp/longjmp\n",
    "\n",
    "![](01.png)\n",
    "![](02.png)\n",
    "\n",
    "[ref1](http://web.eecs.utk.edu/~huangj/cs360/360/notes/Setjmp/lecture.html)\n",
    "\n",
    "------------------------"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## x86 register\n",
    "\n",
    "![](03.png)\n",
    "\n",
    "## call stack frame\n",
    "\n",
    "![](04.png)\n",
    "\n",
    "------------------------"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## linux4.4/arch/x86/um/shared/sysdep/archsetjmp_32.h\n",
    "\n",
    "```c\n",
    "/*\n",
    " * arch/um/include/sysdep-i386/archsetjmp.h\n",
    " */\n",
    "\n",
    "#ifndef _KLIBC_ARCHSETJMP_H\n",
    "#define _KLIBC_ARCHSETJMP_H\n",
    "\n",
    "struct __jmp_buf {\n",
    "\tunsigned int __ebx;\n",
    "\tunsigned int __esp;\n",
    "\tunsigned int __ebp;\n",
    "\tunsigned int __esi;\n",
    "\tunsigned int __edi;\n",
    "\tunsigned int __eip;\n",
    "};\n",
    "\n",
    "typedef struct __jmp_buf jmp_buf[1];\n",
    "\n",
    "#define JB_IP __eip\n",
    "#define JB_SP __esp\n",
    "\n",
    "#endif\t\t\t\t/* _SETJMP_H */\n",
    "\n",
    "```\n",
    "\n",
    "## linux4.4/arch/x86/um/shared/sysdep/archsetjmp_64.h\n",
    "\n",
    "```c\n",
    "/*\n",
    " * arch/um/include/sysdep-x86_64/archsetjmp.h\n",
    " */\n",
    "\n",
    "#ifndef _KLIBC_ARCHSETJMP_H\n",
    "#define _KLIBC_ARCHSETJMP_H\n",
    "\n",
    "struct __jmp_buf {\n",
    "\tunsigned long __rbx;\n",
    "\tunsigned long __rsp;\n",
    "\tunsigned long __rbp;\n",
    "\tunsigned long __r12;\n",
    "\tunsigned long __r13;\n",
    "\tunsigned long __r14;\n",
    "\tunsigned long __r15;\n",
    "\tunsigned long __rip;\n",
    "};\n",
    "\n",
    "typedef struct __jmp_buf jmp_buf[1];\n",
    "\n",
    "#define JB_IP __rip\n",
    "#define JB_SP __rsp\n",
    "\n",
    "#endif\t\t\t\t/* _SETJMP_H */\n",
    "\n",
    "```\n",
    "\n",
    "1. the `jmp_buf` just stores register values\n",
    "\n",
    "---------------------------------------------"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## linux4.4/arch/x86/um/setjmp_32.S\n",
    "\n",
    "```as\n",
    "#\n",
    "# arch/i386/setjmp.S\n",
    "#\n",
    "# setjmp/longjmp for the i386 architecture\n",
    "#\n",
    "\n",
    "#\n",
    "# The jmp_buf is assumed to contain the following, in order:\n",
    "#\t%ebx\n",
    "#\t%esp\n",
    "#\t%ebp\n",
    "#\t%esi\n",
    "#\t%edi\n",
    "#\t<return address>\n",
    "#\n",
    "\n",
    "\t.text\n",
    "\t.align 4\n",
    "\t.globl setjmp\n",
    "\t.type setjmp, @function\n",
    "setjmp:\n",
    "#ifdef _REGPARM\n",
    "\tmovl %eax,%edx\n",
    "#else\n",
    "\tmovl 4(%esp),%edx\n",
    "#endif\n",
    "\tpopl %ecx\t\t\t# Return address, and adjust the stack\n",
    "\txorl %eax,%eax\t\t\t# Return value\n",
    "\tmovl %ebx,(%edx)\n",
    "\tmovl %esp,4(%edx)\t\t# Post-return %esp!\n",
    "\tpushl %ecx\t\t\t# Make the call/return stack happy\n",
    "\tmovl %ebp,8(%edx)\n",
    "\tmovl %esi,12(%edx)\n",
    "\tmovl %edi,16(%edx)\n",
    "\tmovl %ecx,20(%edx)\t\t# Return address\n",
    "\tret\n",
    "\n",
    "\t.size setjmp,.-setjmp\n",
    "\n",
    "\t.text\n",
    "\t.align 4\n",
    "\t.globl longjmp\n",
    "\t.type longjmp, @function\n",
    "longjmp:\n",
    "#ifdef _REGPARM\n",
    "\txchgl %eax,%edx\n",
    "#else\n",
    "\tmovl 4(%esp),%edx\t\t# jmp_ptr address\n",
    "\tmovl 8(%esp),%eax\t\t# Return value\n",
    "#endif\n",
    "\tmovl (%edx),%ebx\n",
    "\tmovl 4(%edx),%esp\n",
    "\tmovl 8(%edx),%ebp\n",
    "\tmovl 12(%edx),%esi\n",
    "\tmovl 16(%edx),%edi\n",
    "\tjmp *20(%edx)\n",
    "\n",
    "\t.size longjmp,.-longjmp\n",
    "\n",
    "```\n",
    "\n",
    "\n",
    "## linux4.4/arch/x86/um/setjmp_64.S\n",
    "\n",
    "```asm\n",
    "#\n",
    "# arch/x86_64/setjmp.S\n",
    "#\n",
    "# setjmp/longjmp for the x86-64 architecture\n",
    "#\n",
    "\n",
    "#\n",
    "# The jmp_buf is assumed to contain the following, in order:\n",
    "#\t%rbx\n",
    "#\t%rsp (post-return)\n",
    "#\t%rbp\n",
    "#\t%r12\n",
    "#\t%r13\n",
    "#\t%r14\n",
    "#\t%r15\n",
    "#\t<return address>\n",
    "#\n",
    "\n",
    "\t.text\n",
    "\t.align 4\n",
    "\t.globl setjmp\n",
    "\t.type setjmp, @function\n",
    "setjmp:\n",
    "\t## xitongsys\n",
    "\t# last element of the stack is the return address (see before call stack frame struct)\n",
    "\tpop  %rsi\t\t\t# Return address, and adjust the stack\n",
    "\n",
    "\t## xitongsys\n",
    "\t# set %eax = 0, is the return value of setjmp\n",
    "\txorl %eax,%eax\t\t\t# Return value\n",
    "\n",
    "\n",
    "\tmovq %rbx,(%rdi)\n",
    "\n",
    "\t## xitongsys\n",
    "\t# we pop the return address before, and now the %rsp is \n",
    "\t# the position before we call setjmp\n",
    "\tmovq %rsp,8(%rdi)\t\t# Post-return %rsp!\n",
    "\n",
    "\t## xitongsys\n",
    "\t# we pop the return address before and we should restore it\n",
    "\tpush %rsi\t\t\t# Make the call/return stack happy\n",
    "\n",
    "\n",
    "\tmovq %rbp,16(%rdi)\n",
    "\tmovq %r12,24(%rdi)\n",
    "\tmovq %r13,32(%rdi)\n",
    "\tmovq %r14,40(%rdi)\n",
    "\tmovq %r15,48(%rdi)\n",
    "\tmovq %rsi,56(%rdi)\t\t# Return address\n",
    "\tret\n",
    "\n",
    "\t.size setjmp,.-setjmp\n",
    "\n",
    "\t.text\n",
    "\t.align 4\n",
    "\t.globl longjmp\n",
    "\t.type longjmp, @function\n",
    "longjmp:\n",
    "\tmovl %esi,%eax\t\t\t# Return value (int)\n",
    "\tmovq (%rdi),%rbx\n",
    "\tmovq 8(%rdi),%rsp\n",
    "\tmovq 16(%rdi),%rbp\n",
    "\tmovq 24(%rdi),%r12\n",
    "\tmovq 32(%rdi),%r13\n",
    "\tmovq 40(%rdi),%r14\n",
    "\tmovq 48(%rdi),%r15\n",
    "\tjmp *56(%rdi)\n",
    "\n",
    "\t.size longjmp,.-longjmp\n",
    "```\n",
    "\n",
    "1. The logic is simple, just save/restore all register values in the jmp_buf. \n",
    "\n",
    "2. See comments in the codes.\n",
    "   \n",
    "-----------------------------"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## setjmp/longjmp used for EXCEPTION HANDLER\n",
    "\n",
    "```c\n",
    "#include <stdio.h>\n",
    "#include <setjmp.h>\n",
    "\n",
    "#define TRY do{ jmp_buf ex_buf__; switch( setjmp(ex_buf__) ){ case 0:\n",
    "#define CATCH(x) break; case x:\n",
    "#define ETRY } }while(0)\n",
    "#define THROW(x) longjmp(ex_buf__, x)\n",
    "\n",
    "#define FOO_EXCEPTION (1)\n",
    "#define BAR_EXCEPTION (2)\n",
    "#define BAZ_EXCEPTION (3)\n",
    "\n",
    "int\n",
    "main(int argc, char** argv)\n",
    "{\n",
    "   TRY\n",
    "   {\n",
    "      printf(\"In Try Statement\\n\");\n",
    "      THROW( BAR_EXCEPTION );\n",
    "      printf(\"I do not appear\\n\");\n",
    "   }\n",
    "   CATCH( FOO_EXCEPTION )\n",
    "   {\n",
    "      printf(\"Got Foo!\\n\");\n",
    "   }\n",
    "   CATCH( BAR_EXCEPTION )\n",
    "   {\n",
    "      printf(\"Got Bar!\\n\");\n",
    "   }\n",
    "   CATCH( BAZ_EXCEPTION )\n",
    "   {\n",
    "      printf(\"Got Baz!\\n\");\n",
    "   }\n",
    "   ETRY;\n",
    "\n",
    "   return 0;\n",
    "}\n",
    "```\n",
    "\n",
    "1. NICE TRICK !!!\n",
    "\n",
    "------------------------------------"
   ]
  }
 ],
 "metadata": {
  "language_info": {
   "name": "python"
  },
  "orig_nbformat": 4
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
